plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

group 'com.chlna6666'
version '1.6.2'

base {
    archivesName = "Ranking"
}

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven {
        name = "PlaceholderAPI"
        url = "https://repo.extendedclip.com/content/repositories/placeholderapi/"
    }
    maven {
        name = "sonatype-oss-snapshots"
        url = "https://oss.sonatype.org/content/repositories/snapshots/"
    }
}

dependencies {
    compileOnly "io.papermc.paper:paper-api:1.20.1-R0.1-SNAPSHOT"
    compileOnly "me.clip:placeholderapi:2.11.5"
    implementation 'fr.mrmicky:fastboard:2.1.5'
    implementation "net.kyori:adventure-platform-bukkit:4.3.4"
    implementation "net.kyori:adventure-api:4.17.0"
    implementation "com.zaxxer:HikariCP:6.2.1"
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

def devServerPluginsDir = "C:\\Users\\Administrator\\Desktop\\leaves\\plugins\\"

tasks.named('shadowJar') {
    archiveClassifier.set("")
    mergeServiceFiles()
    minimize()
    relocate 'fr.mrmicky.fastboard', 'com.chlna6666.ranking.lib.fastboard'
    doLast {
        def targetDir = new File(devServerPluginsDir)
        if (targetDir.exists()) {
            copy {
                from archiveFile
                into targetDir
                rename { String fileName -> "Ranking.jar" }
            }
            println "✅ [本地开发] 插件已成功复制到测试服务器: ${devServerPluginsDir} (重命名为 Ranking.jar)"
        } else {
            println "ℹ️ [CI/构建] 检测到目标目录不存在，已跳过自动复制插件。"
        }
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

tasks.named('shadowJar') {
    dependsOn processResources
}